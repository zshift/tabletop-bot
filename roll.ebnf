<_>              ::= (" " | "\t")*
<Expression>     ::= <Sum> | <RollExpression> | <DiceRoll>
<DiceRoll>       ::= <RollExpression>? "d" <RollExpression> <Keep>? <Drop>?
<RollExpression> ::= <Number> | "(" <_> <Expression> <_> ")"
<Sum>            ::= <Product> <_> <Sum_>?
<Sum_>           ::= <AddOp> <_> <Product> <Sum_>?
<Product>        ::= <Factor> <_> <Product_>? | "-" <_> <Factor>
<Product_>       ::= <MulOp> <_> <Factor> <Product_>?
<Factor>         ::= <RollExpression> | <DiceRoll>
<AddOp>          ::= "+" | "-"
<MulOp>          ::= "*" | "/" | "%"
<KeepLow>        ::= "kl" <RollExpression>
<KeepHigh>       ::= ("k" | "kh") <RollExpression>
<Keep>           ::= <KeepHigh> | <KeepLow>
<DropLow>        ::= ("d" | "dl") <RollExpression>
<DropHigh>       ::= "dh" <RollExpression>
<Drop>           ::= <DropHigh> | <DropLow>
<Number>         ::= <Digit> <Number>?
<Digit>          ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"